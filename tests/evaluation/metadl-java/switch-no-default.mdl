IMPORT('P, "tests/evaluation/metadl-java/src/switch-no-default/", "java5").

ExpectWarning(0), DesireWarning(0), SwitchWithDefault(0), NoWarning(0), DesireNoWarning(0) :- NEQ(0, 0).
EnumMembers(0, 0), CaseOnEnum(0, 0, 0) :- NEQ(0, 0).

analyze('P, "java5") {
### Analysis
	    SwitchWithDefault(s) :- s <: switch (`_) { .. default: .. } :>.
	    Switch(s) :- s <: switch (`_) { .. } :>.

	    CaseOnEnum(s, e, d) :- s <: switch (`v) { .. case `c : .. } :>, TYPE(`v, e), e <: enum `_ { .. } :>, DECL(`c, d).
	    # `m can be either an enum member or an enum constant, use ID to discriminate between the two
	    EnumMember(e, `m) :- e <: enum `_  { .., `m , .. ; .. } :>,  ID(`m, _).

	    SwitchWithoutEnumMember(s, e) :- CaseOnEnum(s, e, _), EnumMember(e, m), NOT(CaseOnEnum(s, e, m)).
	    SwitchOnAllEnumMembers(s) :- CaseOnEnum(s, e, _), NOT(SwitchWithoutEnumMember(s, e)).

	    SwitchWithoutDefault(s) :- Switch(s), NOT(SwitchWithDefault(s)), NOT(SwitchOnAllEnumMembers(s)).
##### Test
	    ParentT(n, c) :- PARENT(n, c).
	    ParentT(n, c) :- ParentT(n, t), PARENT(t, c).

	    ExpectWarning(m) :- m <:  @ExpectWarning("SF_SWITCH_NO_DEFAULT") .. `_ `_  (..) { .. } :>.
	    DesireWarning(m) :- m <:  .. @DesireWarning("SF_SWITCH_NO_DEFAULT") .. `_ `_ (..) { .. } :>.
	    NoWarning(m) :- m <:  .. @NoWarning("SF_SWITCH_NO_DEFAULT") .. `_ `_ (..) { .. } :>.
	    DesireNoWarning(m) :- m <:  .. @DesireNoWarning("SF_SWITCH_NO_DEFAULT") .. `_ `_ (..) { .. } :>.

	    Mismatch(s) :- ExpectWarning(m), Switch(s), NOT(SwitchWithoutDefault(s)), ParentT(m, s).
	    Mismatch(s) :- DesireWarning(m), Switch(s), NOT(SwitchWithoutDefault(s)), ParentT(m, s).

	    # Mismatch(s) :- NoWarning(m), SwitchWithoutDefault(s), ParentT(m, s).
	    Mismatch(s) :- Switch(s), SwitchWithoutDefault(s), NoWarning(m), ParentT(m, s).
	    Mismatch(s) :- Switch(s), SwitchWithoutDefault(s), DesireNoWarning(m), ParentT(m, s).

	    Correct(s) :- ExpectWarning(m), SwitchWithoutDefault(s), ParentT(m, s).
	    Correct(s) :- DesireWarning(m), SwitchWithoutDefault(s), ParentT(m, s).

	    MismatchDetails(file, l, c) :- Mismatch(s), SRC(s, l, c, _, _, file).

	    TestResult("Pass") :- NOT(Mismatch(_)).
	    TestResult("Fail") :- Mismatch(_).
}

OUTPUT('MismatchDetails).
OUTPUT('TestResult).
